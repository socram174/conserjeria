buildscript {
    ext {
        ebeanVersion = '13.25.1'
        sqliteVersion = '3.44.0.0'
        javalinVersion = '5.6.3'

        lombokVersion = '1.18.30'
        gsonVersion = '2.10.1'
        logBackVersion = '1.4.11'
        commonsLangVersion = '3.14.0'
        javaFakerVersion = '1.0.2'

        grpcVersion = '1.59.0'
        protobufVersion = '3.21.12'

        protobufPluginVersion = '0.9.4'
        versionPluginVersion = '0.50.0'
    }
}

plugins {
    id 'idea'
    id 'java'
    id('io.ebean') version "${ebeanVersion}"
    id('com.github.ben-manes.versions') version "${versionPluginVersion}"
    id('com.google.protobuf') version "${protobufPluginVersion}"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'cl.ucn.disc.as'
version = '0.0.5'

repositories {
    mavenLocal()
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}



dependencies {
    // junit
    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'

    // lombok
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // ebean
    testImplementation "io.ebean:ebean-test:${ebeanVersion}"

    // ebean
    implementation "io.ebean:ebean:${ebeanVersion}"
    implementation "io.ebean:ebean-ddl-generator:${ebeanVersion}"
    annotationProcessor "io.ebean:querybean-generator:${ebeanVersion}"

    // database
    implementation "org.xerial:sqlite-jdbc:${sqliteVersion}"
    // implementation 'com.h2database:h2:2.2.220'

    // json
    implementation "com.google.code.gson:gson:${gsonVersion}"

    // web: javalin
    implementation "io.javalin:javalin:${javalinVersion}"

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // logger
    implementation "ch.qos.logback:logback-core:${logBackVersion}"
    implementation "ch.qos.logback:logback-classic:${logBackVersion}"

    // commons-lang3
    implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"

    // data faker
    implementation "com.github.javafaker:javafaker:${javaFakerVersion}"

    // gRPC
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "javax.annotation:javax.annotation-api:1.3.2"

}

ebean {
    debugLevel = 9 //0 - 9
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}

// Inform IntelliJ about the generated code.
idea {
    module {
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/grpc")
    }
}
jar {
    manifest {
        attributes(
                'Main-Class': 'Main'
        )
    }
}

shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'Main'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.grpc.LoadBalancerProvider'
    }
}